" .VIMRC

if version >= 700                   " Ignore configuration on older systems

" -----------------------------------
" Table of Contents
" -----------------------------------
" 1. Vim and Plugins
" 2. User Interface
" 3. Status Line
" 4. Syntax Highlighting
" 5. Text Editing and Searching
" 6. Indentation and Tabs
" 7. Keybindings
" 8. Filetypes and Auto Commands
" 9. Functions and Helpers
" -----------------------------------

" 1. Vim and Plugins ---------------- {{{1

set nocompatible                    " Break backwards compatibility with vi
set encoding=utf-8                  " Character encoding
set ttyfast                         " Optimize for fast terminal connections
set fileformats=unix,dos,mac        " Support all newline formats
set history=1000                    " Increase number of commands saved

set viminfo+=n$HOME/.vim/.viminfo   " Customize path of viminfo file
set backupdir=$HOME/.vim/backup     " Centralize backups
set directory=$HOME/.vim/swap       " Centralize swapfiles

if exists("&undodir")
  set undodir=$HOME/.vim/undo       " Centralize undo history
endif

runtime bundle/vim-pathogen/autoload/pathogen.vim

call pathogen#infect()              " Load plugins in bundle directory
call pathogen#helptags()            " Load bundle help files

" Load matchit.vim which is shipped with vim
if !exists('g:loaded_matchit') && findfile('bundle/matchit.vim', &rtp) ==# ''
  runtime! macros/matchit.vim
endif


" 2. User Interface ----------------- {{{1

if has('gui_running')

  set guifont=Menlo\ Regular:h15    " Select font to use
  set linespace=2                   " Increase spacing between lines
  let g:solarized_bold=0            " Turn off bold fonts

endif

set title                           " Allow vim to customize the title
set number                          " Show line numbers
set cursorline                      " Highlight current line
set hidden                          " Hide modified buffers without complaining
set showmatch                       " Highlight matching parenthesis
set shortmess=atI                   " Shorten messages
set foldmethod=marker               " Enable fold markers

set laststatus=2                    " Always display the status line
set ruler                           " Display cursor position
set showcmd                         " Show currently running command
set noshowmode                      " Mode is displayed in lightline already

set wildmenu                        " Visual autocomplete for command menu
set wildmode=list:longest           " Set wildmeno to list choice

set list                            " Show invisible characters
set listchars=tab:▸\                " Show tab stops
set listchars+=trail:·              " Show trailing spaces
set listchars+=nbsp:_               " Show non breaking spaces
set listchars+=extends:»            " Line continues offscreen
set listchars+=precedes:«           " Line precedes offscreen
"set listchars+=eol:¬               " Show end of line markers

set splitbelow                      " Split to the bottom of the current window
set splitright                      " Split to the right of the current window

set scrolloff=1                     " Min. lines to keep above/below the cursor
set sidescrolloff=5                 " Min. cols to keep left/right of the cursor
set display+=lastline               " Keep as much as possible

set mouse +=a                       " Enable mouse support. Shame on me...
if &term =~ '^screen'
  set ttymouse=xterm2               " Enable extended mouse support.
endif

let g:netrw_banner=0                " Hide the banner in netrw
let g:netrw_altv=1                  " Split window to the right
let g:netrw_alto=1                  " Split window to the bottom


" 3. Status Line -------------------- {{{1

" Customize information displayed in status line
let g:lightline = {
  \ 'colorscheme': 'solarized',
  \ 'active': {
  \   'right': [ [ 'lineinfo' ],
  \              [ 'percent' ],
  \              [ 'fileformat', 'fileencoding' ] ]
  \ },
  \ 'mode_map': {
  \   'n': ' N ',
  \   'i': ' I ',
  \   'R': ' R ',
  \   'v': ' V ',
  \   'V': 'V-L',
  \   'c': ' C ',
  \   "\<C-v>": 'V-B',
  \   's': ' S ',
  \   'S': 'S-L',
  \   "\<C-s>": 'S-B',
  \   '?': '      '
  \ },
  \ 'component_function': {
  \   'fileformat': 'LightLineFileFormat',
  \   'fileencoding': 'LightLineFileEncoding'
  \ },
  \ 'subseparator': { 'left': '', 'right': '·' }
  \ }

" Show file format only if it differs from the default (unix)
function! LightLineFileFormat()
  return &fileformat !=? 'unix' ? &fileformat : ''
endfunction

" Show file encoding only if it differs from the default (utf-8)
function! LightLineFileEncoding()
  let fenc = strlen(&fileencoding) ? &fileencoding : &encoding
  return fenc !=? 'utf-8' ? fenc : ''
endfunction

" Keep custom status line in CtrlP window
let g:ctrlp_status_func = {
  \ 'main': 'CtrlPStatusMain',
  \ 'prog': 'CtrlPStatusProg',
  \ }

function! CtrlPStatusMain(focus, byfname, regex, prev, item, next, marked)
  return lightline#statusline(0)
endfunction

function! CtrlPStatusProg(str)
  return lightline#statusline(0)
endfunction


" 4. Syntax Highlighting ------------ {{{1

if has('syntax') && !exists('g:syntax_on')

  syntax enable                     " Enable syntax processing

  if match($THEME, 'Dark') != -1
    set background=light            " Use dark variant of the theme
    colorscheme solarized           " Select color theme to use
  else
    set background=light            " Use light variant of the theme (default)
    colorscheme solarized           " Select color theme to use
  endif

endif


" 5. Text Editing and Searching ----- {{{1

set nostartofline                   " Do not reset cursor position
set backspace=2                     " Equivalent to backspace=indent,eol,start

set incsearch                       " Highlight dynamically while typing
set ignorecase                      " Ignore case when searching
set smartcase                       " Be case-sensitive when using capitals

set textwidth=80                    " Wrap lines at 80 columns (if enabled)

" Use the silver searcher if avilable
if executable('ag')
  let g:ackprg = 'ag --nogroup --column'
  let g:ctrlp_user_command = 'ag %s -l --nocolor -g ""'
endif


" 6. Indentation and Tabs ----------- {{{1

set autoindent                      " Start indentation at same cursor position
set smarttab                        " Insert blanks according to shiftwidth
set expandtab                       " Expand <Tab>s with spaces
set tabstop=4                       " For proper display of files with tabs
set shiftwidth=2                    " Spaces for each tab stop of (auto)indent
set softtabstop=2                   " Set virtual tab stop
set shiftround                      " Round indents to multiple of shiftwidth

filetype plugin indent on           " Load plugins/indentation per filetype


" 7. Keybindings -------------------- {{{1

" Customize leader key
let mapleader=","

" Avoid the escape key
ino jj <Esc>
ino jk <Esc>

" Allow the use of undo in insert mode
ino <C-u> <C-g>u<C-u>

" Fast file saving
nmap <leader>w :w!<CR>

" Edit mode helpers
cno %% <C-R>=fnameescape(expand('%:h')).'/'<CR>
map <leader>ew :e %%
map <leader>es :split %%
map <leader>ev :vsplit %%
map <leader>et :tabe %%

" Change current directory to the path of the file in the current buffer
nmap <silent> <leader>cd :lcd %:h<CR>:pwd<CR>

" Toggle paste mode
nn <silent> <F4> :set invpaste<CR>:set paste?<CR>
ino <silent> <F4> :set <Esc>:set invpaste<CR>:set paste?<CR>

" Toggle text wrapping
nmap <silent> <leader>tw :set invwrap<CR>:set wrap?<CR>

" Remove trailing whitespace
nmap <leader>rtw :call RemoveTrailingWhitespace()<CR>

" Spell checking
nn <leader>ss :setlocal spell!<CR>
nn <leader>sn ]s
nn <leader>sp [s
nn <leader>sa zg
nn <leader>s? z=

" Convert (word|first) char to (lower|uppercase)
nn <leader>u mQviwU`Q
nn <leader>l mQviwu`Q
nn <leader>U mQgewvU`Q
nn <leader>L mQgewvu`Q

" Remove search highlighting
if maparg('<C-L>', 'n') ==# ''
  nn <silent> <C-l> :nohlsearch<C-R>=has('diff')?'<Bar>diffupdate':''<CR><CR><C-L>
endif

" OSX: Copy and paste to/from clipboard
if has('clipboard') && has('mac')
  vn Y "*y
  nn Y "*yy
  vn <C-c> "*y
  vn <C-v> "*p
  vn <C-x> "*c
endif

" CtrlP: Custom key bindings
" 1. Search word under cursor
" 2. Search file by name
" 3. Search active buffers
nmap <leader>a :Ag! <C-R>=expand('<cword>')<CR><CR>
nmap <leader>p :CtrlP<CR>
nmap <leader>b :CtrlPBuffer<CR>


" 8. Filetypes and Auto Commands ---- {{{1

augroup vimrc_autocmds

  " Remove all existing commands in this group
  au!

  " Remember cursor position, except for commit messages
  au BufReadPost * if &filetype !~ '^git\c' && line("'\"") > 0 && line("'\"") <= line("$")| exe "normal! g`\"" | endif"`'")"'")

  " Text Writing:
  " 1. Wrap lines characters
  " 2. Enable spell checking by default
  au BufNewFile,BufReadPost *.md set filetype=markdown
  au Filetype text,markdown,gitcommit setlocal wrap textwidth=72
  au FileType text,markdown,gitcommit setlocal spell spelllang=en_us,de_de

  " Development:
  " 1. Use tab to expand emmet abbreviation.
  " 2. Remove trailing spaces.
  let g:user_emmet_install_global = 0
  au FileType html,html.twig,php,css,scss,sass EmmetInstall
  au FileType html,html.twig,php,css,scss,sass imap <buffer> <tab> <plug>(emmet-expand-abbr)
  au FileType html,php,css,scss,sass,js autocmd BufWritePre <buffer> :call RemoveTrailingWhitespace()

augroup END


" 9. Functions and Helpers ---------- {{{1

function! RemoveTrailingWhitespace()
  let l:cursor_pos = getpos('.')
  silent! %s/\s\+$//
  call setpos('.', l:cursor_pos)
endfunc

" }}}1

endif " version >= 700

" vim:foldmethod=marker:foldlevel=1
