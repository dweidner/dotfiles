" vimrc                                                                   ><>°
"
" -----------------------------------------------------------------------
" Table of Contents
" -----------------------------------------------------------------------
"  1. Environment
"  2. General settings
"  3. Look & Feel
"  4. Indentation & Tabs
"  5. Search & Replace
"  6. Completion System
"  7. Auto Formatting
"  8. Code Folding
"  9. File Globbing
" 10. Key Mappings
" 11. Plugins
" 12. Auto Commands
" -----------------------------------------------------------------------


" (1) Environment ------------------------------------------------------- {{{1

if empty($XDG_CACHE_HOME)  | let $XDG_CACHE_HOME  = '~/.cache'       | endif
if empty($XDG_CONFIG_HOME) | let $XDG_CONFIG_HOME = '~/.config'      | endif
if empty($XDG_DATA_HOME)   | let $XDG_DATA_HOME   = '~/.local/share' | endif

let g:vimrc_home   = fnamemodify(resolve(expand('$MYVIMRC')), ':p:h')

let s:editor       = has('nvim') ? 'nvim' : 'vim'
let s:cache_home   = expand('$XDG_CACHE_HOME/' . s:editor)
let s:config_home  = expand('$XDG_CONFIG_HOME/' . s:editor)
let s:data_home    = expand('$XDG_DATA_HOME/' . s:editor)


" (2) General settings -------------------------------------------------- {{{1

set lazyredraw                       " No redraw for macros and commands
set synmaxcol=512                    " Reduce number of columns to highlight
set history=1000                     " Increase number of commands saved
set tabpagemax=50                    " Increase number of tabs
set switchbuf+=useopen               " Reuse open buffers
set fileformats=unix,dos,mac         " Enable automatic fileformat detection

set autoread                         " Reload file if modified outside vim
set undofile                         " Save undo history to file
set undolevels=1000                  " Number of changes saved

if !has('nvim')
  set viminfo+=n$XDG_CACHE_HOME/vim/viminfo
  set directory=$XDG_DATA_HOME/vim/swap//
  set undodir=$XDG_DATA_HOME/vim/undo//
  set backupdir=$XDG_DATA_HOME/vim/backup//
endif


" (3) Look & Feel ------------------------------------------------------- {{{1

set title                            " Use filename as window title
set number                           " Show line numbers
set cursorline                       " Highlight current line
set ruler                            " Display cursor position
set showmode                         " Display the current vim mode
set showcmd                          " Show currently running command
set signcolumn="yes"                 " Always display the sign column

set mouse=a                          " Enable mouse support. Shame on me...
set clipboard^=unnamed               " Use system clipboard register
set scrolloff=8                      " Min. lines to keep above/below
set sidescrolloff=16                 " Min. cols to keep left/right

set showtabline=0                    " Hide the tabline by default
set laststatus=2                     " Always display the status line
set signcolumn=yes                   " Always display the sign column
set previewheight=16                 " Increase height of the preview window
set pumheight=10                     " Limit visible entries in popups
set shortmess-=f                     " Stop hiding the file label
set shortmess+=ilmnrotwxIOW          " Abbreviate message output

set nowrap                           " No line wrapping by default
set hidden                           " Hide abandoned buffers 
set nostartofline                    " Do not reset cursor on buffer switch
set nojoinspaces                     " Disable extra spaces
set splitright                       " Split to the right
set splitbelow                       " Split to the bottom

set list                             " Show invisible characters
set listchars=tab:→\                 " Show tab stops
set listchars+=trail:·               " Show trailing spaces
set listchars+=nbsp:⣿                " Show non breaking spaces
set listchars+=extends:»             " Line continues off-screen
set listchars+=precedes:«            " Line precedes off-screen
"set listchars+=eol:¬                " Show end of line markers 

set showbreak=↪\                     " Show line breaks
set fillchars=vert:│                 " Vertical seperator for splits
set fillchars+=diff:⣿                " Fill character for blank lines 

set diffopt=vertical,filler,iwhite   " Use vertical splits, ignore whitespace


" (4) Indentation & Tabs ------------------------------------------------ {{{1

set autoindent                       " Start indentation at same position
set nosmartindent                    " Prefer syntax indentexpr
set nopreserveindent                 " Keep block comments properly aligned
set smarttab                         " Insert blanks according to shiftwidth
set expandtab                        " Expand <Tab>s with spaces
set shiftwidth=2                     " Default to two spaces for indentation
set noshiftround                     " Helps with block comment indentation

set backspace=indent,eol,start       " Delete everything with bs


" (5) Search & Replace -------------------------------------------------- {{{1

set showmatch                         " Highlight matching parenthesis
set incsearch                         " Highlight dynamically while typing
set hlsearch                          " Highlight matches of previous search
set ignorecase                        " Ignore case when searching
set smartcase                         " Be case-sensitive when using capitals

if executable('rg')
  set grepprg=rg\ --vimgrep\ $*
  set grepformat^=%f:%l:%c:%m
elseif executable('ag')
  set grepprg=ag\ --vimgrep\ $*
  set grepformat^=%f:%l:%c:%m
endif


" (6) Completion System ------------------------------------------------- {{{1

set complete-=i                      " Don't scan included files
set complete-=t                      " Avoid tag completion by default
set completeopt+=menu                " Show possible completions in a popup
set completeopt+=menuone             " Show the popup even for single match
set completeopt+=noinsert            " Do not insert any text
set completeopt+=noselect            " Do not select a match in the menu


" (7) Auto Formatting --------------------------------------------------- {{{1

set formatoptions-=t                  " Do not auto-wrap text using textwidth
set formatoptions-=l                  " Don't break long lines in insert mode
set formatoptions-=o                  " Don't insert comment leader after o/O
set formatoptions+=1                  " Break before one letter words
set formatoptions+=2                  " Use indent of 2nd line for paragraphs
set formatoptions+=c                  " Auto-wrap comments using textwidth
set formatoptions+=j                  " Remove comment leader when joining
set formatoptions+=n                  " Recognize and and indent list items
set formatoptions+=q                  " Allow formating of comments with gq
set formatoptions+=r                  " Insert comment leader on new lines

set nrformats-=octal                  " Don't use octals when incrementing


" (8) Code Folding ------------------------------------------------------ {{{1

set nofoldenable                      " Open all folds by default
set foldlevelstart=99


" (9) File Globbing ----------------------------------------------------- {{{1

set wildmenu                          " Enhanced command line completion
set wildmode=list:longest,full        " Set wildmenu to list choice

set wildignore+=*.o,*.obj,*.exe,*.dll,*.jar,*.pyc,*.rbc,*.class
set wildignore+=*.jpg,*.jpeg,*.png,*.gif,*.tiff,*.webp,*.ai,*.psd
set wildignore+=*.avi,*.m4a,*.mp3,*.oga,*.ogg,*.wav,*.webm
set wildignore+=*.pdf,*.doc,*.docx,*.xls,*.xlsx,*.ppt,*.pptx
set wildignore+=*.zip,*.tar,*.tar.gz,*.dmg
set wildignore+=*.*~,*~,._*,*.swp,.lock,.DS_Store,tags
set wildignore+=.sass-cache
set wildignore+=*.min.*,*-min.*


" (10) Key Mappings ----------------------------------------------------- {{{1

" a) General ------------------------------------------------------------ {{{2

" Using space as leader makes sense, as it is easy to reach and does not have
" a special meaning (in vim’s normal mode). Unfortunatly with space as leader
" nothing is displayed, when using showmode. As a workaround we simply map
" space to the default leader key.
map <Space> <Leader>

" Ensure that InsertLeave is fired, when quiting insert mode with Ctrl-c
inoremap <C-c> <Esc>

" Avoid the escape key. i to enter insert mode, ii to leave it
inoremap ii <Esc>

" Expand %% to the same directory of the current file
cnoremap %% <C-R>=fnameescape(expand('%:h')).'/'<CR>

" Reselect visual blocks
xnoremap < <gv
xnoremap > >gv

" qq to record, Q to replay a macro
nmap Q @q

" Toggle the visibility of the colorcolumn
nnoremap <silent> =oC :call dw#ui#ToggleColorColumn()<CR>


" b) Movement ----------------------------------------------------------- {{{2

" Disable arrow keys and take of the training wheels
nnoremap <Up> <Nop>
nnoremap <Right> <Nop>
nnoremap <Down> <Nop>
nnoremap <Left> <Nop>
inoremap <Up> <Nop>
inoremap <Right> <Nop>
inoremap <Down> <Nop>
inoremap <Left> <Nop>

" Move to the beginning/end of the line instead of the top/bottom window
" @see <https://bitbucket.org/sjl/dotfiles>
nnoremap H ^
nnoremap L $

" Jump to the beginning/end of line
inoremap <C-a> <Esc>I
inoremap <C-e> <Esc>A
cnoremap <C-a> <Home>
cnoremap <C-e> <End>

" Faster linewise scrolling
nnoremap <C-e> 3<C-e>
nnoremap <C-y> 3<C-y>

" c) Window/Buffer Management ------------------------------------------- {{{2

" Switch to alternate buffer
nnoremap <Leader><Space> <C-^>

" Circular window navigation
nnoremap <Tab> <C-w>w
nnoremap <S-Tab> <C-w>W

" Open a new vertical/horizontal split
nnoremap <silent> <Leader>v :<C-R>=winwidth(0)/3<CR>vnew<CR>
nnoremap <silent> <Leader>s :<C-R>=winheight(0)/3<CR>new<CR>

" Write the buffer to the current file
nnoremap <Leader>w :update<CR>

" Close the location list and the delete the current buffer
nnoremap <silent> <Leader>q :lclose<CR>:bdelete<CR>

" d) Formatting --------------------------------------------------------- {{{2

" Auto format current buffer
nnoremap <Leader>= ggVG=

" Join lines without spaces
nnoremap <Leader>j VjgJl

" Convert a word to lowercase/uppercase
nnoremap <Leader>l mQviwu`Q
nnoremap <Leader>u mQviwU`Q

" Fill the current line with a given character
nnoremap <silent> <Leader>f- :call dw#FillLine('-')<CR>
nnoremap <silent> <Leader>f. :call dw#FillLine('.')<CR>
inoremap <silent> <C-g>- <C-o>:<C-u>call dw#FillLine('-')<CR><Esc>A
inoremap <silent> <C-g>. <C-o>:<C-u>call dw#FillLine('.')<CR><Esc>A


" (11) Plugins ---------------------------------------------------------- {{{1

" Conditionally load a plugin (@see https://git.io/vd71n)
function! PlugIf(condition, ...) abort
  let l:options = a:condition ? {} : { 'on': [], 'for': [] }
  return a:0 > 0 ? extend(l:options, a:1) : l:options
endfunction

" Determine whether a given feature is supported
function! IsSupported(...) abort
  return call('dw#IsSupported', a:000)
endfunction

" Run the install script in the given plugin directory
function! DoRunInstall(plug) abort
  silent !bash install.sh

  if exists(':UpdateRemotePlugins')
    silent UpdateRemotePlugins
  endif
endfunction

" Use composer to install PHP package dependencies with a plugin
function! DoComposerInstall(plug) abort
  silent !composer install
  silent !composer run-script parse-stubs
endfunction

" Use npm to install node package dependencies with a plugin
function! DoNpmInstall(plug) abort
  silent !npm install
endfunction


call plug#begin(s:data_home . '/bundle')

  " a) Essentials ------------------------------------------------------- {{{2
  " |- ack: Search file contents for patterns
  " |- bbye: Close buffers and don’t mess up window layout
  " |- commentary: Toggle comments
  " |- cylce: Cycle text within predefined candidates
  " |- easyclip: Simplify clipboard functonality
  " |- easydir: Create nonexisting directories when writing files
  " |- endwise: Wisely end language structures
  " |- eunuch: Execute unix shell commands from vim
  " |- fugitive: Execute git commands from vim
  " |- lastplace: Reopen file at last edit position
  " |- lightline: Configurable statusline
  " |- one: Atom one color scheme
  " |- yarp: Yet Another Remote Plugin Framework for Neovim
  " |- repeat: Enable repeating supported plugin maps
  " |- startuptime: Identify slow plugins
  " |- surround: Wrap text objects
  " |- tabular: Text filtering and alignment
  " |- unimpaired: Handy bracket key mappings
  " |- fzf: A fuzzy finder

  Plug 'mileszs/ack.vim'
  Plug 'moll/vim-bbye'
  Plug 'tpope/vim-commentary'
  Plug 'bootleq/vim-cycle', { 'on': [ '<Plug>Cycle' ] }
  Plug 'svermeulen/vim-easyclip', PlugIf(IsSupported('clipboard'))
  Plug 'duggiefresh/vim-easydir'
  Plug 'tpope/vim-endwise'
  Plug 'tpope/vim-eunuch'
  Plug 'tpope/vim-fugitive'
  Plug 'farmergreg/vim-lastplace'
  Plug 'itchyny/lightline.vim'
  Plug 'rakr/vim-one'
  Plug 'roxma/nvim-yarp'
  Plug 'tpope/vim-repeat'
  Plug 'tweekmonster/startuptime.vim', { 'on': [ 'StartupTime' ] }
  Plug 'tpope/vim-surround'
  Plug 'godlygeek/tabular', { 'on': [ 'Tabularize' ] }
  Plug 'tpope/vim-unimpaired'

  if isdirectory('/usr/local/opt/fzf')
    Plug '/usr/local/opt/fzf'
    Plug 'junegunn/fzf.vim'
  endif

  " b) Web Development -------------------------------------------------- {{{2
  " |- editorconfig: Load options from config file
  " |- investigate: Look up documentation (gK)
  " |- neomake: Asynchronous file linting
  " |- neosnippet: Enable snippet manager
  " |- signify: Show file changes in gutter
  " |- splitjoin: Transform singleline to multiline and vice versa (gS/gJ)
  " |- textobj-user: Create custom text objects
  " |- textobj-indent: Select current indent level (i)
  " |- textobj-line: Select current line (l)
  " |- textobj-url: Select urls (u)
  " |- textobj-xmlattr: Select html attributes (x)

  Plug 'ntpeters/vim-better-whitespace', { 'on': [ 'ToggleWhitespace', 'StripWhitespace' ] }
  Plug 'editorconfig/editorconfig-vim'
  Plug 'keith/investigate.vim'
  Plug 'neomake/neomake'
  Plug 'Shougo/neosnippet'
  Plug 'mhinz/vim-signify'
  Plug 'AndrewRadev/splitjoin.vim'

  Plug 'kana/vim-textobj-user'
  Plug 'kana/vim-textobj-indent', { 'on': [ '<Plug>(textobj-indent' ] }
  Plug 'kana/vim-textobj-line', { 'on': [ '<Plug>(textobj-line' ] }
  Plug 'mattn/vim-textobj-url', { 'on': [ '<Plug>(textobj-url' ] }
  Plug 'whatyouhide/vim-textobj-xmlattr', { 'on': [ '<Plug>(textobj-xmlattr' ] }

  " c) Syntax Highlighting & Indentation Rules -------------------------- {{{2
  " |- css3: Syntax highlighting for the latest CSS properties
  " |- git: Filetypes and syntax for gitconfig, gitcommit, etc…
  " |- html5: HTML5 syntax and omnicompletion
  " |- javascript: Improved Javascript indentation and syntax support
  " |- json: Improved JSON highlighting
  " |- jsx: React JSX syntax highlighting and indenting
  " |- less: Syntax for LESS (dynamic CSS)
  " |- liquid: Liquid templates with jekyll additions
  " |- php: Syntax highlighting for PHP 7.x
  " |- php-indent: Latest indentation rules for php
  " |- tmux: Syntax for tmux configuration
  " |- twig: Syntax and indentation for twig templates
  " |- scss: Syntax for SCSS (Sassy CSS)
  " |- vue: Syntax highlighting for Vue components
  " |- yaml: Improved and faster syntax highlighting for yaml

  Plug 'hail2u/vim-css3-syntax'
  Plug 'tpope/vim-git'
  Plug 'othree/html5.vim'
  Plug 'pangloss/vim-javascript'
  Plug 'elzr/vim-json'
  Plug 'mxw/vim-jsx'
  Plug 'groenewege/vim-less'
  Plug 'tpope/vim-liquid'
  Plug 'StanAngeloff/php.vim'
  Plug '2072/PHP-Indenting-for-Vim'
  Plug 'cakebaker/scss-syntax.vim'
  Plug 'tmux-plugins/vim-tmux'
  Plug 'lumiliet/vim-twig'
  Plug 'posva/vim-vue'
  Plug 'stephpy/vim-yaml'

  " d) Completion Manager ---------------------------------------------- {{{2
  " |- ncm2: Slim, fast and hackable completion framework
  " |- ncm2-tern: JavaScript completion using ternjs code analysis
  " |- ncm2-cssomni: CSS completion for CSS, SASS and LESS
  " |- languageclient: Vim support for the Language Server Protocol
  " |- languageserver-php: PHP implementation of the LSP

  Plug 'ncm2/ncm2', PlugIf(IsSupported('completion'))
  Plug 'ncm2/ncm2-path', PlugIf(IsSupported('completion'))
  Plug 'ncm2/ncm2-cssomni', PlugIf(IsSupported('completion'))

  if executable('npm')
    Plug 'ncm2/ncm2-tern', PlugIf(IsSupported('completion'), { 'do': function('DoNpmInstall') })
  endif

  if executable('composer')
    Plug 'autozimu/LanguageClient-neovim', PlugIf(IsSupported('completion'), { 'branch': 'next', 'do': function('DoRunInstall') })
    Plug 'roxma/LanguageServer-php-neovim', PlugIf(IsSupported('completion'), { 'do': function('DoComposerInstall') })
  endif

  " e) Bookmarks ------------------------------------------------------- {{{2
  " |- ale: Check syntax and fix files, with LSP support
  " |- vim-cool: Disable search highlighting when you are done searching
  " |- vim-ripgrep: Use RipGrep and display results in a quickfix list

  " Plug 'dense-analysis/ale'
  " Plug 'romainl/vim-cool'

  " if executable('rg')
  "   Plug 'jremmen/vim-ripgrep'
  " endif

call plug#end()


" (12) Auto Commands ---------------------------------------------------- {{{1

augroup vimrc

  autocmd!

  " Check if any buffers were changed outside of vim
  autocmd FocusGained * if &autoread | silent checktime | endif

  " Temporarily hide the cursorline
  autocmd WinLeave,InsertEnter * call dw#ui#DisableTemporarily('%', 'cursorline')
  autocmd WinEnter,InsertLeave * call dw#ui#Restore('%', 'cursorline')

  " Temporarily hide search results when entering insert mode
  autocmd InsertEnter * call dw#ui#DisableTemporarily('%', 'hlsearch')
  autocmd InsertLeave * call dw#ui#Restore('%', 'hlsearch')

augroup END


" }}}1


set secure


" vim:foldmethod=marker:foldlevel=2
