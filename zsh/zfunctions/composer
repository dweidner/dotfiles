# zsh/zfunctions/composer

#
# Disable xdebug extension and run composer.
#
# Usage:
# composer command [options] [arguments]
#
function composer() {
  local s

  local version
  version=$(php -v | grep -E "PHP [57]" | sed 's/.*PHP \([^-]*\).*/\1/' | cut -c 1-6)

  local src=/usr/local/etc/php/${version%.*}/conf.d
  local target=/usr/local/etc/php/${version%.*}/conf.disabled

  if (( $+commands[composer] == 0 )); then
    echo "Could not find composer in path" >&2
    return 1
  fi

  if [[ ! -f "$src/ext-xdebug.ini" ]]; then
    command composer "$@"
    return 0
  fi

  if [[ ! -d "$target" ]]; then
    mkdir "$target"
  fi

  mv "$src/ext-xdebug.ini" "$target/"

  command composer "$@"
  s=$?

  mv "$target/ext-xdebug.ini" "$src"

  return $s
}

# vim:syntax=zsh
