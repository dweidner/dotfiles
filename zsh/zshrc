# zsh/zshrc

# ----------------------------------------------------
# Table of Contents
# ----------------------------------------------------
# 1. Dotfiles Setup
# 2. Sensible Defaults
# 3. Command History
# 4. DIR Colors
# 5. ZSH Functions
# 6. ZSH Plugins
# 7. ZSH Configuration
# 8. Misc
# ----------------------------------------------------


# 1. Dotfiles Setup ---------------------------------- {{{1

export DOTFILES="$HOME/.dotfiles"  # Expose the dotfiles directory
source "$DOTFILES/shell/helpers"   # Load frequently used utility functions


# 2. Sensible Defaults ------------------------------- {{{1

ttyctl -f                  # Restore terminal settings when exiting abnormally

setopt AUTO_PUSHD          # Make cd push the old directory onto the stack
setopt PUSHD_TO_HOME       # Have pushd with no arguments act like 'pushd ~'
setopt PUSHD_SILENT        # Do not print the directory stack after pushd/popd
setopt PUSHD_IGNORE_DUPS   # Do not store duplicates in stack
setopt EXTENDED_GLOB       # Use extended globbing syntax
setopt NOTIFY              # Report status of background jobs immediately
setopt LONG_LIST_JOBS      # List jobs in the long format by default

unsetopt BEEP              # Disable BEEP on error
unsetopt HUP               # Don't kill jobs on shell exit
unsetopt CHECK_JOBS        # Don't report on jobs when exiting shell
unsetopt CLOBBER           # Do not overwrite files with > and >>


# 3. Command History ---------------------------------- {{{1

HISTFILE="$HOME/.zsh_history"  # Customize the path of the zsh history
HISTCONTROL=ignoreboth         # Ignore duplicates and lines starting with space
HISTSIZE=1000                  # Max number of entries saved in internal history
SAVEHIST=1000                  # Max number of entries saved in history file

setopt EXTENDED_HISTORY        # Add timestamp and duration to history
setopt SHARE_HISTORY           # Read and write commands to history
setopt INC_APPEND_HISTORY      # Immediately write to the history file
setopt HIST_VERIFY             # Do not exuecute immediately on history expansion
setopt HIST_IGNORE_SPACE       # Ignore command lines with a leading space
setopt HIST_IGNORE_DUPS        # Ignore duplicates of previous line
setopt HIST_IGNORE_ALL_DUPS    # Delete old record if new one is a duplicate
setopt HIST_SAVE_NO_DUPS       # Avoid saving duplicates to the history file
setopt HIST_EXPIRE_DUPS_FIRST  # Remove duplicate entries from history before
                               # losing a unique event


# 4. DIR Colors -------------------------------------- {{{1

# Load zsh color module
autoload -U colors && colors

# Customize colorized output of the `list directory contents` command
if [[ -f "$HOME/.dircolors" && (( $+commands[dircolors] )) ]]; then
  eval "$(dircolors --sh "$HOME/.dircolors")"
elif [[ -f "/etc/dircolors" && (( $+commands[dircolors] )) ]]; then
  eval "$(dircolors --sh "/etc/dircolors")"
elif [[ "$OSTYPE" == darwin* ]]; then
  export CLICOLOR=1
  export LSCOLORS=gxfxbEaEBxxEhEhBaDaCaD
fi


# 5. ZSH Functions ----------------------------------- {{{1

# Add the custom function directory to the search path
fpath=( "$DOTFILES/zsh/functions" "$DOTFILES/zsh/completions" $fpath )

# Load all custom functions automatically upon their
# first reference
for file in "$DOTFILES/zsh/functions/"*; do
  autoload -Uz ${file##*/}
done

# Clean up temporary variables
unset file


# 6. ZSH Plugins ------------------------------------- {{{1

# Set the sequence of the plugins to load
zsh_plugins=(
  zsh-syntax-highlighting
  zsh-history-substring-search
  zsh-completions
  pure
  bd
)

# Autoload ZSH plugins specified in the collection
for plugin in $zsh_plugins; do
  plugin_path="$DOTFILES/zsh/plugins/$plugin"
  for plugin_file in "$plugin_path/$plugin"{.plugin,}.zsh; do
    if [ -s "$plugin_file" ]; then
      source "$plugin_file"
      break
    fi
  done
done

# Cleanup temporary variables
unset zsh_plugins plugin{,_path,_file}


# 7. ZSH Configuration ------------------------------- {{{1

# Load additional configuration files
zsh_config_files=(
  "$DOTFILES/zsh/zcompletion"
  "$DOTFILES/zsh/zprompt"
  "$DOTFILES/zsh/zabbr"
  "$DOTFILES/zsh/zkeymaps"
  "$DOTFILES/shell/aliases"
  "$HOME/.zshrc_local"
)

for config_file in $zsh_config_files; do
  [ -s "$config_file" ] && source "$config_file"
done

# Cleanup temporary variables
unset zsh_config_files config_file


# 8. Misc -------------------------------------------- {{{1

# Load node version manager
if brew list "nvm" >/dev/null 2>&1; then
  source "$(brew --prefix nvm)/nvm.sh"
fi

# Suggest installation of a command with homebrew
if brew command command-not-found-init >/dev/null 2>&1; then
  eval "$(brew command-not-found-init)"
fi

# Enable magic quoting of urls
autoload -U url-quote-magic
zle -N self-insert url-quote-magic

# Autoload the run-help function for zsh builtin commands
unalias run-help
autoload -Uz run-help
alias help="run-help"

# Automatically remove duplicates from these path variables
typeset -U path cdpath fpath manpath


# vim:syntax=zsh:foldmethod=marker:foldlevel=2
