# zsh/zshrc

# -------------------------------------
# Table of Contents
# -------------------------------------
# 1. Dotfiles Setup
# 2. Sensible Defaults
# 3. Command History
# 4. DIR Colors
# 5. ZSH Functions
# 6. ZSH Plugins
# 7. ZSH Configuration
# 8. Misc
# -------------------------------------


# 1. Dotfiles Setup ------------------- {{{1

export DOTFILES="$HOME/.dotfiles"  # Expose the dotfiles directory
source "$DOTFILES/shell/helpers"   # Load frequently used utility functions
source "$DOTFILES/shell/aliases"   # Load shell aliases
source "$DOTFILES/shell/functions" # Load shell functions


# 2. Sensible Defaults ---------------- {{{1

stty -ixon                 # Disable start/stop output control
ttyctl -f                  # Restore terminal settings when exiting abnormally
bindkey -e                 # Use emacs-like key bindings by default

setopt AUTO_PUSHD          # Make cd push the old directory onto the stack
setopt PUSHD_TO_HOME       # Have pushd with no arguments act like 'pushd ~'
setopt PUSHD_SILENT        # Do not print the directory stack after pushd/popd
setopt PUSHD_IGNORE_DUPS   # Do not store duplicates in stack
setopt EXTENDED_GLOB       # Use extended globbing syntax
setopt NOTIFY              # Report status of background jobs immediately
setopt LONG_LIST_JOBS      # List jobs in the long format by default

unsetopt BEEP              # Disable BEEP on error
unsetopt HUP               # Don't kill jobs on shell exit
unsetopt CHECK_JOBS        # Don't report on jobs when exiting shell
unsetopt CLOBBER           # Do not overwrite files with > and >>


# 3. Command History ------------------ {{{1

HISTFILE="$HOME/.zhistory"     # Customize the path of the zsh history
HISTCONTROL=ignoreboth         # Ignore duplicates and lines starting with space
HISTSIZE=1000                  # Max number of entries saved in internal history
SAVEHIST=1000                  # Max number of entries saved in history file

setopt EXTENDED_HISTORY        # Add timestamp and duration to history
setopt SHARE_HISTORY           # Read and write commands to history
setopt INC_APPEND_HISTORY      # Immediately write to the history file
setopt HIST_VERIFY             # Do not exuecute immediately on history expansion
setopt HIST_IGNORE_SPACE       # Ignore command lines with a leading space
setopt HIST_IGNORE_DUPS        # Ignore duplicates of previous line
setopt HIST_IGNORE_ALL_DUPS    # Delete old record if new one is a duplicate
setopt HIST_SAVE_NO_DUPS       # Avoid saving duplicates to the history file
setopt HIST_EXPIRE_DUPS_FIRST  # Remove duplicate entries from history before
                               # losing a unique event


# 4. DIR Colors ----------------------- {{{1

autoload -U colors && colors

if [ -f "$HOME/.dircolors" ] && (( $+commands[dircolors] )); then
  eval "$(dircolors --sh "$HOME/.dircolors")"
elif (( $+commands[dircolors] )); then
  eval "$(dircolors --sh "$DOTFILES/shell/dircolors")"
elif is_mac; then
  export CLICOLOR=1
  export LSCOLORS=gxfxbEaEBxxEhEhBaDaCaD
fi


# 5. ZSH Functions -------------------- {{{1

fpath=( "$HOME/.zfunctions" "$DOTFILES/zsh/zfunctions" $fpath )
fpath=( "$HOME/.zcompletions" "$DOTFILES/zsh/zcompletions" $fpath )

for file ($DOTFILES/zsh/zfunctions/*(N)); do
  autoload -Uz ${file##*/}
done

unset file


# 6. ZSH Plugins ---------------------- {{{1

#
# Helper function to load zplug packages.
#
# Usage: zplug_load
#
zplug_load() {
  source "$ZPLUG_HOME/init.zsh"

  local file
  [ -s "$HOME/.zpackages" ] && file="$HOME/.zpackages" || file="$DOTFILES/zsh/zpackages"
  [ -s "$file" ] && source "$file"

  if ! zplug check --verbose; then
    printf "Install missing zsh plugins? [y/N] "
    if read -q; then
      echo; zplug install
    fi
  fi

  zplug load
}

# Load zsh plugins if plugin manager is installed
[[ -s "$ZPLUG_HOME/init.zsh" ]] && zplug_load

# Cleanup shell environment
unset -f zplug_load


# 7. ZSH Configuration ---------------- {{{1

for file ($DOTFILES/zsh/config/*.zsh(N)); do
  source "$file"
done

unset file


# 8. Misc ----------------------------- {{{1

if [ -s "$HOME/.zlocal" ]; then
  source "$HOME/.zlocal"
fi

typeset -U path cdpath fpath manpath


# vim:syntax=zsh:foldmethod=marker:foldlevel=2
